# model 3: AR(1) with yearly cycling
model {  
   # priors on parameters
   mu0 ~ dnorm(log(meanY), 0.25); 
   tau ~ dgamma(1,0.1); 
   sd <- 1/sqrt(tau);
   phi ~ dnorm(0,1) T(-1, 1);
   beta1 ~ dnorm(0, 1/(2.5^2));
   beta2 ~ dnorm(0, 1/(2.5^2));

   X[1] <- mu0;
   predY[1] <- exp(X[1]);
   Y[1] ~ dpois(exp(X[1]));

   for(i in 2:N) {
      predX[i] <- phi*X[i-1] + beta1*cosyr[i] + beta2*sinyr[i]; 
      checkX[i] ~ dnorm(predX[i], tau); # Process variation
      X[i] <- min(c(checkX[i], 18)); # Enforce ceiling to avoid overflow
      predY[i] <- exp(X[i]);
      Y[i] ~ dpois(exp(X[i])); # Observation variation
   }
}  
